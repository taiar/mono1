
To show some more interesting features of Ceylon, I wrote a version of the
Producer-consumer problem. This is a classic example of a multi-process
synchronization problem. It describes two processes, the producer and the
consumer, who share a common, fixed-size storage (buffer) used as a queue. The
producer's job is to generate a piece of data, put it into the storage and start
again. At the same time, the consumer is consuming the data (i.e., removing it
from the storage) one piece at a time. The problem is to make sure that the
producer won't try to add data into the storage if it's full and that the
consumer won't try to remove data from an empty storage\cite{2_1}.

The implementation relies strongly of the Java Thread libraries. The Producer
and the Consumer objects runs on different threads, each instance of each
object. The Storage class manage the additions of the producers and the
remotion of the consumers on the buffer. For synchronization I used the Java's
Semaphore class.

Let's see the code and I'll give the explanations:

\begin{lstlisting}[label=cpc,caption=Ceylon Producer-Consumer example]
import ceylon.collection { ArrayList }
import java.util.concurrent { Semaphore }
import java.lang { Thread, Math }

shared Integer getRandomInteger(Integer a, Integer b) {
	value range = b - a + 1;
	value fraction = (range * Math.random());
	return (fraction + a).integer;
}

class Producer(Storage storage, shared actual Integer id) extends Thread() {

	void produce() {
		if(Math.random() < 0.5) {
			storage.add(this);
		}
	}

	shared actual void run() {
		while(true) {
			this.produce();
			this.sleep(getRandomInteger(1, 4) * 1000);
		}
	}

}

class Consumer(Storage storage, shared actual Integer id) extends Thread() {

	void consume() {
		if(Math.random()  < 0.5) {
			storage.get(this);
		}
	}

	shared actual void run() {
		while(true) {
			this.consume();
			this.sleep(getRandomInteger(1, 4) * 1000);
		}
	}

}

class Storage(shared Integer storageSpaces) {

	value buffer = ArrayList<Integer>(storageSpaces);
	variable Integer lastEmpty = 0;
	value m = Semaphore(1);

	for(i in 0..(this.storageSpaces - 1)) {
		this.buffer.push(0);
	}

	shared void get(Producer|Consumer actor) {
		m.acquire();
		assert(actor is Consumer);
		print("[Consumer " + actor.id.string + "] Wants to consume!");
		if(this.lastEmpty == 0) {
			print("[Storage] I have nothing for you now. Look: ");
		} else {
			this.lastEmpty--;
			this.buffer.set(this.lastEmpty, 0);
			print("[Storage] Ok, I got a thing for you.");
		}
		m.release();
		this.printBuffer();
	}

	shared void add(Producer|Consumer actor) {
		m.acquire();
		assert(actor is Producer);
		print("[Producer " + actor.id.string + "] Produced something.");
		if(this.lastEmpty == this.storageSpaces) {
			print("[Storage] I'm full! Can't take it right now, look:");
		} else {
			this.buffer.set(this.lastEmpty, 1);
			this.lastEmpty++;
			print("[Storage] Tank you! I'll store it.");
		}
		m.release();
		this.printBuffer();
	}

	shared void printBuffer() {
		m.acquire();
		process.write("[ ");
		for (load in this.buffer) {
			process.write(load.string + " ");
		}
		print("]");
		m.release();
	}

}

shared void run() {
	value storage = Storage(15);

	value p1 = Producer(storage, 1);
	value p2 = Producer(storage, 2);

	value c1 = Consumer(storage, 1);
	value c2 = Consumer(storage, 2);

	p1.start();
	p2.start();

	c1.start();
	c2.start();
}
\end{lstlisting}

The first thing we see are the imports. We are already used to them, in the
first article where I used already some Java interoperability and explained the
concept of modules. Every of these things are used here.

\subsection{Toplevel functions}

After that, we can see a function called \textit{getRandomInteger}. In Ceylon, this is a
\textit{toplevel function}. A toplevel function declaration, or a function declaration
nested inside the body of a containing class or interface, may be annotated
\textit{shared} (in the last article, I talked about the shared annotation and the
visibility issues). A toplevel shared function is visible wherever the package
that contains it is visible.

Another interesting thing about \textit{toplevel} functions in Ceylon is that they can
be called by external programs on the system. On the example, I'll modify the
function a little so we can see what is going on:

\begin{lstlisting}[label=cri,caption=Ceylon random integer function]
shared Integer getRandomInteger(Integer a = 1, Integer b = 5) {
	value range = b - a + 1;
	value fraction = (range * Math.random());
	value gen = (fraction + a).integer;
	print(gen);
	return gen;
}
\end{lstlisting}


The toplevel function must have no parameters (or default value parameters) so
you can call her externally. By placing our program inside a module called
\textit{prodcons} (see the module session on the first article), we can compile
and run the program like:

\begin{verbatim}
$ ceylon compile
$ ceylon run --run prodcons::getRandomInteger prodcons
\end{verbatim}

The random integer numbers between $[1, 5[$ will be generated by the program and
printed on the screen.

\subsection{Classes}

We have then, the class \textit{Producer} which is the very same class of the
\textit{Consumer}, except it calls different methods of the \textit{Storage} class. The
first interesting thing we can see is that Ceylon 1.1 has no constructor
methods. Since the earliest versions of the language, it supports a streamlined
syntax for class initialization where the parameters of a class are listed right
after the class name, and initialization logic goes directly in the body of the
class\cite{2_2}.

We can instantiate the class Producer like this:

\begin{lstlisting}[label=cpc,caption=Ceylon Producer-Consumer example]
value prod = Producer(Storage(15), 1);
\end{lstlisting}

The ability to refer to parameters of the class directly from the members of the
class has the intuit to cut down on verbosity. However, there are moments when
we would really appreciate the ability to write a class with multiple
initialization paths, something like constructors in Java. The constructors are
being implemented on Ceylon and will be available in the next versions of the
language.

The annotation \textit{shared} on the parameter \textbf{id} says that this is like a Java's
\textit{public} member of the class. \textbf{storage} is not annotated, so it is like a
private one.

Look at the annotation \textit{actual} that is used in the same \textbf{id} parameter and in
the \textbf{run} method. It tells that, inside the inheritance tree of possible
values (since the two classes extends the Thread Java class) that could
overwrite the method or the variable, this is the very one that will do it. So,
\textbf{shared id} is overwriting the parameter id (probably on the Thread Java
class) and \textbf{shared run} is overwriting a run method (surely on the Thread Java
class). In the case of Interfaces, the word to tell that a class implements
(from Java) an interface is \textbf{satisfies}; so a class \textbf{satisfies} an
interface. The annotation \textbf{actual} is used to tell what method is
\textbf{satisfying} the Interface's specification.

\subsection{Collections, sequences and tuples}

Ceylon SDK has a great library that implements every kind of collections, just
like Java does. There are interfaces and classes to implement all sort of
operations involving \textit{ArrayList}, \textit{LinkedList}, 
\textit{PriorityQueue}, \textit{HashSet}, \textit{HashMap}, \textit{TreeSet},
\textit{TreeMap} etc\cite{2_3}. In our example, I used an \textit{ArrayList}
(which is the implementation of a list using arrays) to store the production of
the Producer.

In the example, I used a loop to initialize every cell of the \textit{buffer}
list with the value zero. In the for loop, I used a \textbf{Sequence} to
generate the iterable value. Sequence is a type that in the first time could
look very familiar to a Java array but in fact they are very different. First of
all, the sequence is a \textbf{immutable} type and not a mutable concrete type
like the array. We can't set the value of an element like:

\begin{lstlisting}[label=cpc,caption=Ceylon Producer-Consumer example]
String[] operators = .... ;
operators[0] = "^"; //compile error
\end{lstlisting}

This following code, doesn't compile too:

\begin{lstlisting}[label=cpc,caption=Ceylon Producer-Consumer example]
for (i in 0..operators.size-1) {
    String op = operators[i]; //compile error
    // ...
}
\end{lstlisting}

The index operation $operators[i]$ returns an optional type \textit{String?},
which cannot be assigned to the type \textit{String}. Instead, if we need access
to the index, we use the special form of \textbf{for}:

\begin{lstlisting}[label=cpc,caption=Ceylon Producer-Consumer example]
for (i -> op in operators.indexed) {
    // ...
}
\end{lstlisting}

Ceylon has the \textbf{tuple} type too. It might be a very common use for the
most of those who already worked with a language that has tuples.

\begin{lstlisting}[label=cpc,caption=Ceylon Producer-Consumer example]
[Float,Float,String] point = [0.0, 0.0, "origin"];
\end{lstlisting}

\subsection{Type system}

Every value in a Ceylon program is an instance of a type that can be expressed
within the Ceylon language as a class. The language does not define any
primitive or compound types that cannot, in principle, be expressed within the
language itself.

Each class declaration defines a type. However, not all types are classes. It is
often advantageous to write generic code that abstracts the concrete class of a
value. This technique is called polymorphism. Ceylon features two different
kinds of polymorphism:

\begin{enumerate}
	\item \textbf{subtype polymorphism}, where a subtype \textit{B} inherits a supertype \textit{A}, and 
	\item \textbf{parametric polymorphism}, where a type definition \textit{A<T>} is parameterized by a generic type parameter T.
\end{enumerate}

Ceylon, like Java and many other object-oriented languages, features a single
inheritance model for classes. A class may directly inherit at most one other
class, and all classes eventually inherit, directly or indirectly, the class
\textit{Anything} defined in the module \textit{ceylon.language}, which acts as the root of
the class hierarchy.

In our example, we use a parameter of the methods \textit{add} and  \textit{get} which is
\textit{Producer|Consumer} type. This type means that, whateaver a \textit{Producer} or a
\textit{Consumer} parameter passed the this method, it'll work. The methods doesn't
need this, I place'em there just for exemplification. In Ceylon, this is called
\textbf{Union types}. For any types \textit{X} and \textit{Y}, the union, or disjunction, \textit{X|Y}, of
the types may be formed. A union type is a supertype of both of the given types
\textit{X} and \textit{Y}, and an instance of either type is an instance of the union type.

\subsection{Assertions and exceptions}

The assert statement validates a given condition, throwing an
\textit{AssertionException} if the condition is not satisfied. A distinguishing
characteristic of Ceylon is that exceptions aren't used to represent programming
errors. The Ceylon creators thinks that exceptions like \textit{NullPointerException},
\textit{ClassCastException} and \textit{IndexOutOfBoundsException} should never occur in at
runtime in a production system. They represent problems that must be fixed by
the programmer editing code, tend to hide the \"corner\" condition they represent
from someone reading the code and are much too low-level to carry any useful
information about the real problem. Because of that, Ceylon tries to encode
these \"corner\" conditions into the type system.
The compiler won't let you write:

\begin{lstlisting}[label=cpc,caption=Ceylon Producer-Consumer example]
print(process.arguments[1].uppercased);
\end{lstlisting}

This code isn't well-typed because $process.arguments[1]$ is of type
\textit{String?}, reflecting the fact that there might not be a second element in the
$list process.arguments$. Instead you're forced to at least take into
account the possibility that there are less than two arguments:

\begin{lstlisting}[label=cpc,caption=Ceylon Producer-Consumer example]
if (exists arg = process.arguments[1]) {
    print(arg.uppercased);
}
else {
    throw Exception("missing second argument");
}
\end{lstlisting}

Obviously, the code is a little bigger than the initial code we had but of
course the problem is very much clear, semantic and the readers of the code
would understand the problem behind the size of the arguments in a much clearer
way.

\subsection{Concurrency}

In our example, inside of the methods \textit{get} and \textit{add} of the \textit{Storage} class is
where we would have concurrency problems\cite{2_1}. To solve this potential
problems, the class uses synchronization implemented with semaphores. I used the
Semaphore class from Java \textit{"acquiring"} and \textit{"releasing"} the
execution flow whatever some of the Threads are updating the \textit{buffer} or
printing in the screen.

In Java is very common to use the \textit{synchronized} method annotation to tell a
Thread that this method have race conditions, and the JVM takes care of the
concurrency. To use it with Ceylon, I had to specifically put the call to the
synchronization methods because it doesn't have the annotation nor any kind of
compatibility with it.